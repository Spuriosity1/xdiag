cmake_minimum_required(VERSION 3.19)

find_package(HDF5 COMPONENTS CXX)

set(xdiag_distributed_known_comps static shared)
set(xdiag_distributed_comp_static NO)
set(xdiag_distributed_comp_shared NO)
foreach (xdiag_distributed_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (xdiag_distributed_comp IN_LIST xdiag_distributed_known_comps)
        set(xdiag_distributed_comp_${xdiag_distributed_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xdiag_distributed does not recognize component `${xdiag_distributed_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (xdiag_distributed_comp_static AND xdiag_distributed_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "xdiag_distributed `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(xdiag_distributed_static_targets "${CMAKE_CURRENT_LIST_DIR}/xdiag_distributed-static-targets.cmake")
set(xdiag_distributed_shared_targets "${CMAKE_CURRENT_LIST_DIR}/xdiag_distributed-shared-targets.cmake")

macro(xdiag_distributed_load_targets type)
    if (NOT EXISTS "${xdiag_distributed_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xdiag_distributed `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${xdiag_distributed_${type}_targets}")
endmacro()

if (xdiag_distributed_comp_static)
    xdiag_distributed_load_targets(static)
elseif (xdiag_distributed_comp_shared)
    xdiag_distributed_load_targets(shared)
elseif (DEFINED xdiag_distributed_SHARED_LIBS AND xdiag_distributed_SHARED_LIBS)
    xdiag_distributed_load_targets(shared)
elseif (DEFINED xdiag_distributed_SHARED_LIBS AND NOT xdiag_distributed_SHARED_LIBS)
    xdiag_distributed_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${xdiag_distributed_shared_targets}")
        xdiag_distributed_load_targets(shared)
    else ()
        xdiag_distributed_load_targets(static)
    endif ()
else ()
    if (EXISTS "${xdiag_distributed_static_targets}")
        xdiag_distributed_load_targets(static)
    else ()
        xdiag_distributed_load_targets(shared)
    endif ()
endif ()
