cmake_minimum_required(VERSION 3.19)

find_package(OpenMP)
find_package(HDF5 COMPONENTS CXX)

set(xdiagjl_known_comps static shared)
set(xdiagjl_comp_static NO)
set(xdiagjl_comp_shared NO)
foreach (xdiagjl_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (xdiagjl_comp IN_LIST xdiagjl_known_comps)
        set(xdiagjl_comp_${xdiagjl_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Xdiagjl does not recognize component `${xdiagjl_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (xdiagjl_comp_static AND xdiagjl_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "Xdiagjl `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(xdiagjl_static_targets "${CMAKE_CURRENT_LIST_DIR}/xdiagjl-static-targets.cmake")
set(xdiagjl_shared_targets "${CMAKE_CURRENT_LIST_DIR}/xdiagjl-shared-targets.cmake")

macro(xdiagjl_load_targets type)
    if (NOT EXISTS "${xdiagjl_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xdiagjl `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${xdiagjl_${type}_targets}")
endmacro()

if (xdiagjl_comp_static)
    xdiagjl_load_targets(static)
elseif (xdiagjl_comp_shared)
    xdiagjl_load_targets(shared)
elseif (DEFINED xdiagjl_SHARED_LIBS AND xdiagjl_SHARED_LIBS)
    xdiagjl_load_targets(shared)
elseif (DEFINED xdiagjl_SHARED_LIBS AND NOT xdiagjl_SHARED_LIBS)
    xdiagjl_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${xdiagjl_shared_targets}")
        xdiagjl_load_targets(shared)
    else ()
        xdiagjl_load_targets(static)
    endif ()
else ()
    if (EXISTS "${xdiagjl_static_targets}")
        xdiagjl_load_targets(static)
    else ()
        xdiagjl_load_targets(shared)
    endif ()
endif ()
