cmake_minimum_required(VERSION 3.19)

find_package(OpenMP)
  
set(xdiag_known_comps static shared)
set(xdiag_comp_static NO)
set(xdiag_comp_shared NO)
foreach (xdiag_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (xdiag_comp IN_LIST xdiag_known_comps)
        set(xdiag_comp_${xdiag_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "XDiag does not recognize component `${xdiag_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (xdiag_comp_static AND xdiag_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "XDiag `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(xdiag_static_targets "${CMAKE_CURRENT_LIST_DIR}/xdiag-static-targets.cmake")
set(xdiag_shared_targets "${CMAKE_CURRENT_LIST_DIR}/xdiag-shared-targets.cmake")

macro(xdiag_load_targets type)
    if (NOT EXISTS "${xdiag_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xdiag `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${xdiag_${type}_targets}")
endmacro()

if (xdiag_comp_static)
    xdiag_load_targets(static)
elseif (xdiag_comp_shared)
    xdiag_load_targets(shared)
elseif (DEFINED xdiag_SHARED_LIBS AND xdiag_SHARED_LIBS)
    xdiag_load_targets(shared)
elseif (DEFINED xdiag_SHARED_LIBS AND NOT xdiag_SHARED_LIBS)
    xdiag_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${xdiag_shared_targets}")
        xdiag_load_targets(shared)
    else ()
        xdiag_load_targets(static)
    endif ()
else ()
    if (EXISTS "${xdiag_static_targets}")
        xdiag_load_targets(static)
    else ()
        xdiag_load_targets(shared)
    endif ()
endif ()
